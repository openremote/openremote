apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"

dependencies {
    api "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    api "com.fasterxml.jackson.module:jackson-module-parameter-names:$jacksonVersion"

    api "org.hibernate.orm:hibernate-core:$hibernateVersion"
    api "org.hibernate.validator:hibernate-validator:$hibernateValidatorVersion"
    api "jakarta.el:jakarta.el-api:$javaxELAPIVersion"
    api "jakarta.annotation:jakarta.annotation-api:$javaxAnnotationsVersion"
    api "jakarta.ws.rs:jakarta.ws.rs-api:$jaxrsSpecVersion"
    api "org.jboss.resteasy:resteasy-cache-core:4.7.10.Final"

    api "org.keycloak:keycloak-core:$keycloakVersion"

    api "org.geotools:gt-main:$geotoolsVersion"
    api "com.jayway.jsonpath:json-path:$jaywayJsonPathVersion"

    api "org.jeasy:easy-rules-core:$easyRulesVersion"
    api "org.mnode.ical4j:ical4j:$ical4jVersion"
    api "org.reflections:reflections:$reflectionsVersion"
    api "com.google.guava:guava:$guavaVersion"

    api "org.checkerframework:checker-qual:$checkerframeworkVersion"

    api "io.swagger.core.v3:swagger-annotations:$swaggerVersion"

    api "com.github.victools:jsonschema-generator:$victoolsJsonSchemaGeneratorVersion"
    api "com.github.victools:jsonschema-module-jackson:$victoolsJsonSchemaGeneratorVersion"
    api "com.github.victools:jsonschema-module-jakarta-validation:$victoolsJsonSchemaGeneratorVersion"

    api "io.github.classgraph:classgraph:$classgraphVersion"
    api "io.micrometer:micrometer-core:$micrometerVersion"
    api "com.devskiller.friendly-id:friendly-id:$friendlyIdVersion"
    api "com.fasterxml.uuid:java-uuid-generator:$uuidGeneratorVersion"

    api "fr.neolegal:exp4j:$exp4jVersion"

    dependencies {
        testImplementation 'org.skyscreamer:jsonassert:1.5.3'
        testImplementation "org.junit.jupiter:junit-jupiter-params:$junitVersion"
        testImplementation "org.junit.jupiter:junit-jupiter-api:$junitVersion"
        testImplementation "org.mockito:mockito-core:$mockitoVersion"
        testRuntimeOnly 'org.glassfish:jakarta.el:4.0.2'
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junitVersion"
        testRuntimeOnly "org.junit.platform:junit-platform-launcher"
    }
}

tasks.withType(Test) {
    environment("OR_LOGGING_CONFIG_FILE", "logging-dev.properties")
}

test {}

base {
    archivesName = "openremote-${project.name}"
}

jar {
    from sourceSets.main.allJava
}

javadoc {
    failOnError = false
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            group = "io.openremote"
            artifactId = "openremote-${project.name}"
            from components.java
            pom {
                name = 'OpenRemote Model'
                description = 'Provides core model classes used within OpenRemote; add maven {url "https://repo.osgeo.org/repository/release/"} and maven {url "https://pkgs.dev.azure.com/OpenRemote/OpenRemote/_packaging/OpenRemote/maven/v1"} to resolve all dependencies'
                url = 'https://github.com/openremote/openremote'
                licenses {
                    license {
                        name = 'GNU Affero General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'developers'
                        name = 'Developers'
                        email = 'developers@openremote.io'
                        organization = 'OpenRemote'
                        organizationUrl = 'https://openremote.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/openremote/openremote.git'
                    developerConnection = 'scm:git:ssh://github.com:openremote/openremote.git'
                    url = 'https://github.com/openremote/openremote/tree/master'
                }
            }
        }
    }

    repositories {
        maven {
            if (!version.endsWith('-LOCAL')) {
                credentials {
                    username = findProperty("publishUsername")
                    password = findProperty("publishPassword")
                }
            }
            url = version.endsWith('-LOCAL') ? layout.buildDirectory.dir('repo') : version.endsWith('-SNAPSHOT') ? findProperty("snapshotsRepoUrl") : findProperty("releasesRepoUrl")
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}
