import com.fasterxml.jackson.annotation.JsonAutoDetect
import cz.habarta.typescript.generator.Jackson2Configuration

apply plugin: "groovy"
apply plugin: "cz.habarta.typescript-generator"

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "cz.habarta.typescript-generator:typescript-generator-gradle-plugin:$typescriptGeneratorVersion"
    }
}

dependencies {
    compile resolveProject(":model")
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"    
    compile "cz.habarta.typescript-generator:typescript-generator-core:$typescriptGeneratorVersion"
}

generateTypeScript {
    jsonLibrary = "jackson2"
    classPatterns = [
            "org.openremote.model.**"
    ]
    excludeClassPatterns = [
            "org.openremote.model.http.*Constraint**",
            "org.openremote.model.event.shared.*Filter**",
            "org.openremote.model.util.**",
            "org.openremote.model.flow.**",
            "org.openremote.model.interop.**",
            "org.openremote.model.value.impl.**"
    ]
    customTypeNamingFunction = "function(name, simpleName) { if (name.indexOf(\"\$\") > 0) return name.substr(name.lastIndexOf(\".\")+1).replace(\"\$\",\"\"); }"
    customTypeMappings = [
            "org.openremote.model.value.ObjectValue:{ [id: string]: any }",
            "org.openremote.model.value.ArrayValue:any[]",
            "org.openremote.model.value.NumberValue:number",
            "org.openremote.model.value.StringValue:string",
            "org.openremote.model.value.BooleanValue:boolean",
            "org.openremote.model.value.Value:any",
            "org.openremote.model.attribute.Meta:MetaItem[]"
    ]
    optionalProperties = "all" // TODO: cleanup model to be more explicit about optional params
    mapEnum = cz.habarta.typescript.generator.EnumMapping.asEnum
    outputFile = "src/index.ts"
    outputFileType = "implementationFile"
    outputKind = "module"
    jackson2Configuration = [
        fieldVisibility: "ANY",
        creatorVisibility: "ANY",
        getterVisibility: "NONE",
        isGetterVisibility: "NONE",
        setterVisibility: "NONE"
    ]
    jackson2Modules = [
        "com.fasterxml.jackson.datatype.jdk8.Jdk8Module",
        "org.openremote.model.ModelModule"
    ]
    extensionsWithConfiguration = [
            new cz.habarta.typescript.generator.Settings.ConfiguredExtension(
                className: "EnumWithInterfacesExtension",
//                configuration: [
//                        enumPattern1: "**AssetType**",
//                        enumPattern2: "**AttributeType**",
//                        enumPattern3: "**AttributeValueType**",
//                        enumPattern4: "**MetaItemType**",
//                ]
            )
    ]
}

build.dependsOn generateTypeScript, npmBuild
npmBuild.dependsOn generateTypeScript

clean {
    doLast {
        def dir = new File("${projectDir}/dist")
        dir.deleteDir()
    }
}

task prepareUi() {
    dependsOn clean, npmPrepare
}

task publishUi() {
    dependsOn clean, npmPublish
}