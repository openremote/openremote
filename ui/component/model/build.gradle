buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "cz.habarta.typescript-generator:typescript-generator-gradle-plugin:$typescriptGeneratorVersion"
    }
}

plugins {
    id 'groovy'
    id 'cz.habarta.typescript-generator'
}

dependencies {
    if (isCustomProject()) {
        // TODO: Remove this as shouldn't build custom project code into this package
        def customModel = findProject(":model")
        if (customModel != null) {
            implementation customModel
        }
        def customAgent = findProject(":agent")
        if (customAgent != null) {
            implementation customAgent
        }
    }
    compileOnly resolveProject(":model-util")
    implementation resolveProject(":model")
    implementation resolveProject(":agent")
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:$jacksonVersion"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:$jacksonVersion"
    implementation "com.fasterxml.jackson.module:jackson-module-parameter-names:$jacksonVersion"
    implementation "cz.habarta.typescript-generator:typescript-generator-core:$typescriptGeneratorVersion"
}

generateTypeScript createTSGeneratorConfigForModel("src/model.ts")

build.dependsOn generateTypeScript, npmBuild
npmBuild.dependsOn generateTypeScript

clean {
    doLast {
        def dir = new File("${projectDir}/dist")
        dir.deleteDir()
    }
}

task prepareUi() {
    dependsOn clean, npmPrepare
}

task publishUi() {
    dependsOn clean, npmPublish
}
