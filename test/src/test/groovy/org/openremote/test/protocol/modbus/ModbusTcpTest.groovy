/*
 * Copyright 2025, OpenRemote Inc.
 *
 * See the CONTRIBUTORS.txt file in the distribution for a
 * full listing of individual contributors.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program. If not, see <http://www.gnu.org/licenses/>.
 */
package org.openremote.test.protocol.modbus

import net.solarnetwork.io.modbus.BitsModbusMessage
import net.solarnetwork.io.modbus.ModbusBlockType
import net.solarnetwork.io.modbus.ModbusErrorCode
import net.solarnetwork.io.modbus.ModbusFunctionCode
import net.solarnetwork.io.modbus.ModbusMessage
import net.solarnetwork.io.modbus.RegistersModbusMessage
import net.solarnetwork.io.modbus.netty.msg.BaseModbusMessage
import net.solarnetwork.io.modbus.tcp.netty.NettyTcpModbusServer
import org.openremote.agent.protocol.modbus.ModbusAgent
import org.openremote.agent.protocol.modbus.ModbusAgentLink
import org.openremote.agent.protocol.modbus.ModbusSerialProtocol
import org.openremote.agent.protocol.modbus.ModbusTcpAgent
import org.openremote.agent.protocol.modbus.ModbusTcpProtocol
import org.openremote.manager.agent.AgentService
import org.openremote.manager.asset.AssetProcessingService
import org.openremote.manager.asset.AssetStorageService
import org.openremote.model.asset.agent.Agent
import org.openremote.model.asset.agent.ConnectionStatus
import org.openremote.model.asset.impl.ShipAsset
import org.openremote.model.asset.impl.ThingAsset
import org.openremote.model.attribute.Attribute
import org.openremote.model.attribute.AttributeEvent
import org.openremote.model.attribute.MetaItem
import org.openremote.model.attribute.MetaMap
import org.openremote.model.value.ValueType
import org.openremote.test.ManagerContainerTrait
import spock.lang.Shared
import spock.lang.Specification
import spock.util.concurrent.PollingConditions

import java.util.concurrent.atomic.AtomicReference

import static net.solarnetwork.io.modbus.ModbusFunctionCodes.WRITE_COIL
import static net.solarnetwork.io.modbus.ModbusFunctionCodes.WRITE_COILS
import static net.solarnetwork.io.modbus.ModbusFunctionCodes.WRITE_HOLDING_REGISTER
import static net.solarnetwork.io.modbus.ModbusFunctionCodes.WRITE_HOLDING_REGISTERS
import static net.solarnetwork.io.modbus.netty.msg.BitsModbusMessage.readCoilsResponse
import static net.solarnetwork.io.modbus.netty.msg.BitsModbusMessage.readDiscretesResponse
import static net.solarnetwork.io.modbus.netty.msg.BitsModbusMessage.writeCoilResponse
import static net.solarnetwork.io.modbus.netty.msg.BitsModbusMessage.writeCoilsResponse
import static net.solarnetwork.io.modbus.netty.msg.RegistersModbusMessage.*
import static org.openremote.model.Constants.MASTER_REALM
import static org.openremote.model.value.MetaItemType.AGENT_LINK

class ModbusTcpTest extends Specification implements ManagerContainerTrait {

    @Shared
    int modbusServerPort

    @Shared
    NettyTcpModbusServer server

    @Shared
    AtomicReference<ModbusMessage> latestReadMessage = new AtomicReference(null)

    @Shared
    AtomicReference<ModbusMessage> latestWriteMessage = new AtomicReference(null)
//    @Shared
//    Map<, Number> messageMap = [:]

    def setupSpec() {
        modbusServerPort = findEphemeralPort()
        server = new NettyTcpModbusServer(modbusServerPort)
        server.setMessageHandler((msg, sender) -> {
            ModbusMessage modbusMessage = null
            switch (msg.getFunction().blockType()) {
                case ModbusBlockType.Coil -> {
                    BitsModbusMessage tcpRequest = msg.unwrap(BitsModbusMessage.class)
                    if (msg.getFunction().isReadFunction()) {
                        modbusMessage = readCoilsResponse(tcpRequest.getUnitId(), tcpRequest.getAddress(), tcpRequest.getCount(), BigInteger.valueOf(0xFFFF))
                    } else {
                        if (tcpRequest.getFunction().getCode() == WRITE_COILS) {
                            modbusMessage = writeCoilsResponse(tcpRequest.getUnitId(), tcpRequest.getAddress(), tcpRequest.getCount())
                        } else if (tcpRequest.getFunction().getCode() == WRITE_COIL) {
                            modbusMessage = writeCoilResponse(tcpRequest.getUnitId(), tcpRequest.getAddress(), true)
                        }
                    }
                }
                case ModbusBlockType.Discrete -> {
                    BitsModbusMessage tcpRequest = msg.unwrap(BitsModbusMessage.class)
                    modbusMessage = readDiscretesResponse(tcpRequest.getUnitId(), tcpRequest.getAddress(), tcpRequest.getCount(), BigInteger.valueOf(0xFFFF))
                }
                case ModbusBlockType.Holding -> {
                    RegistersModbusMessage registerRequest = msg.unwrap(RegistersModbusMessage.class)
                    if (msg.getFunction().isReadFunction()) {
                        // Generate mock data based on address and count
                        short[] mockData = generateMockHoldingData(registerRequest.getAddress(), registerRequest.getCount())
                        println("TCP Mock: address=${registerRequest.getAddress()}, count=${registerRequest.getCount()}, data=${mockData.collect{String.format('0x%04X', it & 0xFFFF)}}")
                        modbusMessage = readHoldingsResponse(registerRequest.getUnitId(), registerRequest.getAddress(), mockData)
                    } else {
                        if (registerRequest.getFunction().getCode() == WRITE_HOLDING_REGISTERS) {
                            modbusMessage = writeHoldingsResponse(registerRequest.getUnitId(), registerRequest.getAddress(), registerRequest.getCount())
                        } else if (registerRequest.getFunction().getCode() == WRITE_HOLDING_REGISTER) {
                            modbusMessage = writeHoldingResponse(registerRequest.getUnitId(), registerRequest.getAddress(), 21)
                        }
                    }
                }
                case ModbusBlockType.Input -> {
                    RegistersModbusMessage registerRequest = msg.unwrap(RegistersModbusMessage.class)
                    modbusMessage = readInputsResponse(registerRequest.getUnitId(), registerRequest.getAddress(), new short[]{11, 12, 13, 14, 15, 16, 17, 18, 19, 20})
                }
                case ModbusBlockType.Diagnostic -> {
                    modbusMessage = new BaseModbusMessage(msg.getUnitId(), msg.getFunction(), ModbusErrorCode.Acknowledge)
                }
            }

            if (msg.getFunction().isReadFunction()) {
                latestReadMessage.set(msg)
            } else {
                latestWriteMessage.set(msg)
            }
            sender.accept(modbusMessage)

    })
        server.start()
    }

    def cleanupSpec() {
        server.stop()
    }

    /**
     * Generate mock holding register data based on address and count.
     * This simulates different test data for different register ranges.
     */
    static short[] generateMockHoldingData(int address, int count) {
        short[] data = new short[count]

        // Basic registers (0-9): simple sequential values
        // 64-bit test registers (100-111): specific values for LINT, ULINT, LREAL
        // Byte/Word order test registers (200-207): values to test endianness

        for (int i = 0; i < count; i++) {
            // PLC4X uses 0-indexed addresses, so address 99 = register 100 in 1-indexed notation
            int registerAddress = address + i + 1

            if (registerAddress >= 1 && registerAddress <= 10) {
                // Basic sequential values for simple tests
                data[i] = (short)(registerAddress)
            } else if (registerAddress >= 100 && registerAddress < 112) {
                // 64-bit data type test values
                // LINT at 100-103 (4 registers): value = 1234567890123456L
                // ULINT at 104-107 (4 registers): value = 9876543210987654L
                // LREAL at 108-111 (4 registers): value = 123.456789

                if (registerAddress >= 100 && registerAddress <= 103) {
                    // LINT: 1234567890123456L = 0x000462D53C8ABAC0
                    long lintValue = 1234567890123456L
                    int regIndex = registerAddress - 100
                    data[i] = (short)((lintValue >> (48 - regIndex * 16)) & 0xFFFF)
                } else if (registerAddress >= 104 && registerAddress <= 107) {
                    // ULINT: 9876543210987654L = 0x00231D31F8D09706
                    long ulintValue = 9876543210987654L
                    int regIndex = registerAddress - 104
                    data[i] = (short)((ulintValue >> (48 - regIndex * 16)) & 0xFFFF)
                } else if (registerAddress >= 108 && registerAddress <= 111) {
                    // LREAL: 123.456789 = 0x405EDD2F1A9FBE77
                    long lrealBits = Double.doubleToRawLongBits(123.456789)
                    int regIndex = registerAddress - 108
                    data[i] = (short)((lrealBits >> (48 - regIndex * 16)) & 0xFFFF)
                }
            } else if (registerAddress >= 200 && registerAddress < 208) {
                // Byte/Word order test values
                // INT32 at 200-201: value = 0x12345678
                // FLOAT at 202-203: value = 12.34f
                // INT32 at 204-205: value = 0xABCDEF01
                // FLOAT at 206-207: value = 56.78f

                if (registerAddress == 200) {
                    data[i] = (short)0x1234
                } else if (registerAddress == 201) {
                    data[i] = (short)0x5678
                } else if (registerAddress == 202 || registerAddress == 203) {
                    // FLOAT 12.34f = 0x4145C28F
                    int floatBits = Float.floatToRawIntBits(12.34f)
                    data[i] = (short)((floatBits >> ((203 - registerAddress) * 16)) & 0xFFFF)
                } else if (registerAddress == 204) {
                    data[i] = (short)0xABCD
                } else if (registerAddress == 205) {
                    data[i] = (short)0xEF01
                } else if (registerAddress == 206 || registerAddress == 207) {
                    // FLOAT 56.78f = 0x42633d71
                    int floatBits = Float.floatToRawIntBits(56.78f)
                    data[i] = (short)((floatBits >> ((207 - registerAddress) * 16)) & 0xFFFF)
                }
            } else {
                // Default: return address-based value
                data[i] = (short)(registerAddress % 100)
            }
        }

        return data
    }

    def "Modbus TCP Integration Test - Basic Operations"() {
        given: "expected conditions"
        def conditions = new PollingConditions(timeout: 10, delay: 0.2)

        when: "the container starts"
        def container = startContainer(defaultConfig(), defaultServices())
        def assetStorageService = container.getService(AssetStorageService.class)
        def assetProcessingService = container.getService(AssetProcessingService.class)
        def agentService = container.getService(AgentService.class)

        and: "a mock Modbus agent is created"
        def agent = new ModbusTcpAgent("Modbus")
        agent.setRealm(MASTER_REALM)

        agent.setHost("127.0.0.1")
        agent.setPort(modbusServerPort)

        agent = assetStorageService.merge(agent)

        then: "the protocol instance for the agent should be created"
        conditions.eventually {
            assert agentService.getProtocolInstance(agent.id) != null
            assert ((ModbusTcpProtocol)agentService.getProtocolInstance(agent.id)) != null
        }

        and: "the connection status should be CONNECTED"
        conditions.eventually {
            agent = assetStorageService.find(agent.getId())
            agent.getAttribute(Agent.STATUS).get().getValue().get() == ConnectionStatus.CONNECTED
        }

        when: "a device with partial read configuration is created (missing readValueType)"
        def device = new ThingAsset("Partial Config Device")
        device.setRealm(MASTER_REALM)
        device.addOrReplaceAttributes(
                // Partial read config - has readMemoryArea and readAddress but missing readValueType
                new Attribute<>("partialReadConfig", ValueType.INTEGER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadAddress(550)
                                    // Missing: readValueType
                                }
                        )
                )
        )
        device = assetStorageService.merge(device)

        then: "the attribute should be linked without creating read polling tasks"
        conditions.eventually {
            def protocol = agentService.getProtocolInstance(agent.id) as ModbusTcpProtocol
            assert protocol != null
            // No batch groups should be created for incomplete read config
            assert protocol.batchGroups.isEmpty()
        }

        when: "A ShipAsset is created with an agent link"
        ShipAsset ship = new ShipAsset("testAsset")
        ship.setRealm(MASTER_REALM)
        ship.addOrReplaceAttributes(new Attribute<Object>(ShipAsset.SPEED).addOrReplaceMeta(new MetaItem<>(
                AGENT_LINK,
                new ModbusAgentLink(
                        id: agent.getId(),
                        unitId: 1,
                        requestInterval: 1000,
                        readMemoryArea: ModbusAgentLink.ReadMemoryArea.HOLDING,
                        readValueType: ModbusAgentLink.ModbusDataType.UINT,
                        readAddress: 2,
                        writeMemoryArea: ModbusAgentLink.WriteMemoryArea.HOLDING,
                        writeAddress: 3
                )
        )))
        ship = assetStorageService.merge(ship)
        ModbusAgentLink agentLink


        then: "a client should be created and the pollingMap is populated"
        conditions.eventually {
            def protocol = agentService.getProtocolInstance(agent.id) as ModbusTcpProtocol
            assert protocol != null
            assert protocol.batchGroups.size() > 0

            ship = assetStorageService.find(ship.getId(), true)
            agentLink = ship.getAttribute(ShipAsset.SPEED).get().getMetaItem(AGENT_LINK).get().getValue(ModbusAgentLink.class).get()
            assert ship.getAttribute(ShipAsset.SPEED).flatMap { it.getValue() }.orElse(null) == 2

            assert (latestReadMessage.get() as ModbusMessage).getUnitId() === 1
            assert (latestReadMessage.get() as ModbusMessage).getFunction() === ModbusFunctionCode.ReadHoldingRegisters
            assert (latestReadMessage.get() as ModbusMessage).unwrap(RegistersModbusMessage.class).getAddress() == agentLink.getReadAddress()-1
            assert (latestReadMessage.get() as ModbusMessage).unwrap(RegistersModbusMessage.class).getCount() == 1
        }

        when: "the attribute is updated"
        assetProcessingService.sendAttributeEvent(new AttributeEvent(ship.getId(), ShipAsset.SPEED, 123D))
        ship.addOrReplaceAttributes(new Attribute<?>(ShipAsset.SPEED, 10))

        then: "the value is sent to the Modbus server"
        conditions.eventually {
            def msg = (latestWriteMessage.get() as ModbusMessage).unwrap(RegistersModbusMessage)
            assert msg != null
            assert msg.getCount() == 1
            assert msg.getAddress() == agentLink.getWriteAddress()-1
            assert msg.dataDecodeUnsigned() == [123] as int[]

            ship = assetStorageService.find(ship.getId(), true)
            assert ship.getAttribute(ShipAsset.SPEED).get().getValue().orElse(0) == 2.0
        }

        when: "I add a new coil attribute and remove the Agent Link from the speed attribute"
        ship.addOrReplaceAttributes(new Attribute<Integer>("coil1", ValueType.BOOLEAN).addOrReplaceMeta(new MetaItem<>(
                AGENT_LINK,
                new ModbusAgentLink(
                        id: agent.getId(),
                        unitId: 1,
                        requestInterval: 1000,
                        readMemoryArea: ModbusAgentLink.ReadMemoryArea.COIL,
                        readValueType: ModbusAgentLink.ModbusDataType.BOOL,
                        readAddress: 5,
                        writeMemoryArea: ModbusAgentLink.WriteMemoryArea.COIL,
                        writeAddress: 6
                )
        )))
        ship.addOrReplaceAttributes(new Attribute<Object>(ShipAsset.SPEED).setMeta(Map.of() as MetaMap))
        latestReadMessage.set(null)
        latestWriteMessage.set(null)

        and: "I merge it"
        ship = assetStorageService.merge(ship)

        then: "I should receive the correct value in the coil"

        conditions.eventually {
            agentLink = ship.getAttribute("coil1").get().getMetaItem(AGENT_LINK).get().getValue(ModbusAgentLink.class).get()

            assert agentService.getProtocolInstance(agent.id) != null
            assert ((ModbusTcpProtocol)agentService.getProtocolInstance(agent.id)) != null

            def msg = (latestReadMessage.get() as ModbusMessage).unwrap(BitsModbusMessage)
            assert msg != null
            assert msg.getCount() == 1
            assert msg.getAddress() == agentLink.getReadAddress()-1

            ship = assetStorageService.find(ship.getId(), true)
            assert ship.getAttribute("coil1").flatMap { it.getValue() }.orElse(null) == true
        }

        when: "I update the value of the coil"

        assetProcessingService.sendAttributeEvent(new AttributeEvent(ship.getId(), "coil1", true))

        then: "the coil is read properly"

        conditions.eventually {
            def msg = (latestWriteMessage.get() as ModbusMessage).unwrap(BitsModbusMessage)
            assert msg != null
            assert msg.getCount() == 1
            assert msg.getAddress() == 5
            assert msg.getBits() == BigInteger.valueOf(0x0001)

            ship = assetStorageService.find(ship.getId(), true)
            assert ship.getAttribute("coil1").flatMap { it.getValue() }.orElse(null) == true
        }
    }

    def "Modbus TCP Test - Register Batching"() {
        given: "expected conditions"
        def conditions = new PollingConditions(timeout: 10, delay: 0.2)

        when: "the container starts"
        def container = startContainer(defaultConfig(), defaultServices())
        def assetStorageService = container.getService(AssetStorageService.class)
        def agentService = container.getService(AgentService.class)

        and: "a Modbus TCP agent with batching enabled is created"
        def agent = new ModbusTcpAgent("Modbus TCP Batching")
        agent.setRealm(MASTER_REALM)
        agent.setHost("127.0.0.1")
        agent.setPort(modbusServerPort)
        agent.addOrReplaceAttributes(
                new Attribute<>(ModbusAgent.DEVICE_CONFIG, [
                        "default": new ModbusAgent.ModbusDeviceConfig(ModbusAgent.EndianFormat.BIG_ENDIAN, "", 50)
                ] as ModbusAgent.DeviceConfigMap) // Enable batching
        )
        agent = assetStorageService.merge(agent)

        then: "the protocol instance should be created and connected"
        conditions.eventually {
            assert agentService.getProtocolInstance(agent.id) != null
            agent = assetStorageService.find(agent.getId())
            agent.getAttribute(Agent.STATUS).get().getValue().get() == ConnectionStatus.CONNECTED
        }

        when: "a device with multiple sequential register attributes is created"
        ThingAsset device = new ThingAsset("Batching Test Device")
        device.setRealm(MASTER_REALM)
        device.addOrReplaceAttributes(
                new Attribute<>("register1", ValueType.POSITIVE_INTEGER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.UINT)
                                    it.setReadAddress(1)
                                }
                        )
                ),
                new Attribute<>("register2", ValueType.POSITIVE_INTEGER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.UINT)
                                    it.setReadAddress(2)
                                }
                        )
                ),
                new Attribute<>("register3", ValueType.POSITIVE_INTEGER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.UINT)
                                    it.setReadAddress(3)
                                }
                        )
                )
        )
        device = assetStorageService.merge(device)

        then: "batch groups should be created and attributes should receive values"
        conditions.eventually {
            def protocol = agentService.getProtocolInstance(agent.id) as ModbusTcpProtocol
            assert protocol != null

            // Check that batch groups were created (batching is enabled)
            assert protocol.batchGroups.size() > 0

            device = assetStorageService.find(device.getId(), true)

            // Verify all attributes received values from the batch read
            assert device.getAttribute("register1").flatMap { it.getValue() }.isPresent()
            assert device.getAttribute("register2").flatMap { it.getValue() }.isPresent()
            assert device.getAttribute("register3").flatMap { it.getValue() }.isPresent()

            // Verify the values match what the mock server returns
            assert device.getAttribute("register1").flatMap { it.getValue() }.get() == 1
            assert device.getAttribute("register2").flatMap { it.getValue() }.get() == 2
            assert device.getAttribute("register3").flatMap { it.getValue() }.get() == 3
        }
    }

    def "Modbus TCP Test - 64-bit Data Types (LINT, ULINT, LREAL)"() {
        given: "expected conditions"
        def conditions = new PollingConditions(timeout: 10, delay: 0.2)

        when: "the container starts"
        def container = startContainer(defaultConfig(), defaultServices())
        def assetStorageService = container.getService(AssetStorageService.class)
        def agentService = container.getService(AgentService.class)

        and: "a Modbus TCP agent is created with batching enabled"
        def agent = new ModbusTcpAgent("Modbus TCP 64-bit Test")
        agent.setRealm(MASTER_REALM)
        agent.setHost("127.0.0.1")
        agent.setPort(modbusServerPort)
        agent.addOrReplaceAttributes(
                new Attribute<>(ModbusAgent.DEVICE_CONFIG, [
                        "default": new ModbusAgent.ModbusDeviceConfig(ModbusAgent.EndianFormat.BIG_ENDIAN, "", 50)
                ] as ModbusAgent.DeviceConfigMap)
        )
        agent = assetStorageService.merge(agent)

        then: "the agent should connect successfully"
        conditions.eventually {
            agent = assetStorageService.find(agent.getId())
            agent.getAttribute(Agent.STATUS).get().getValue().get() == ConnectionStatus.CONNECTED
        }

        when: "a device with 64-bit data types is created"
        def device = new ThingAsset("64-bit Test Device")
        device.setRealm(MASTER_REALM)
        device.addOrReplaceAttributes(
                // LINT - 64-bit signed integer (4 registers) at 100-103
                new Attribute<>("longSignedValue", ValueType.LONG).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.LINT)
                                    it.setReadAddress(100)
                                    it.setRegistersAmount(4)
                                }
                        )
                ),
                // ULINT - 64-bit unsigned integer (4 registers) at 104-107
                new Attribute<>("longUnsignedValue", ValueType.BIG_INTEGER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.ULINT)
                                    it.setReadAddress(104)
                                    it.setRegistersAmount(4)
                                }
                        )
                ),
                // LREAL - Double precision float (4 registers) at 108-111
                new Attribute<>("doubleValue", ValueType.NUMBER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.LREAL)
                                    it.setReadAddress(108)
                                    it.setRegistersAmount(4)
                                }
                        )
                )
        )
        device = assetStorageService.merge(device)

        then: "all 64-bit attributes should receive values"
        conditions.eventually {
            device = assetStorageService.find(device.getId(), true)

            // Verify all attributes have values
            assert device.getAttribute("longSignedValue").flatMap { it.getValue() }.isPresent()
            assert device.getAttribute("longUnsignedValue").flatMap { it.getValue() }.isPresent()
            assert device.getAttribute("doubleValue").flatMap { it.getValue() }.isPresent()

            // Verify the exact values from mock server (PLC4X converts multi-register data)
            assert device.getAttribute("longSignedValue").flatMap { it.getValue() }.get() == 1234567890123456L
            assert device.getAttribute("longUnsignedValue").flatMap { it.getValue() }.get() == 9876543210987654L
            assert Math.abs(device.getAttribute("doubleValue").flatMap { it.getValue() }.get() as Double - 123.456789) < 0.000001
        }

        and: "verify batching was used"
        conditions.eventually {
            def protocol = agentService.getProtocolInstance(agent.id) as ModbusTcpProtocol
            assert protocol != null
            assert protocol.batchGroups.size() > 0
        }
    }

    def "Modbus TCP Test - Byte and Word Order"() {
        given: "expected conditions"
        def conditions = new PollingConditions(timeout: 10, delay: 0.2)

        when: "the container starts"
        def container = startContainer(defaultConfig(), defaultServices())
        def assetStorageService = container.getService(AssetStorageService.class)
        def agentService = container.getService(AgentService.class)

        and: "a Modbus TCP agent with BIG-BIG byte/word order is created"
        def agent = new ModbusTcpAgent("Modbus TCP Endian Test")
        agent.setRealm(MASTER_REALM)
        agent.setHost("127.0.0.1")
        agent.setPort(modbusServerPort)
        agent.addOrReplaceAttributes(
                new Attribute<>(ModbusAgent.DEVICE_CONFIG, [
                        "default": new ModbusAgent.ModbusDeviceConfig(ModbusAgent.EndianFormat.BIG_ENDIAN, "", 1)
                ] as ModbusAgent.DeviceConfigMap)
        )
        agent = assetStorageService.merge(agent)

        then: "the agent should connect successfully"
        conditions.eventually {
            agent = assetStorageService.find(agent.getId())
            agent.getAttribute(Agent.STATUS).get().getValue().get() == ConnectionStatus.CONNECTED
        }

        when: "a device with INT32 and FLOAT values is created"
        def device = new ThingAsset("Endian Test Device")
        device.setRealm(MASTER_REALM)
        device.addOrReplaceAttributes(
                new Attribute<>("int32Value", ValueType.INTEGER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.DINT)
                                    it.setReadAddress(200)
                                    it.setRegistersAmount(2)
                                }
                        )
                ),
                new Attribute<>("floatValue", ValueType.NUMBER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.REAL)
                                    it.setReadAddress(202)
                                    it.setRegistersAmount(2)
                                }
                        )
                )
        )
        device = assetStorageService.merge(device)

        then: "the values should be read correctly with BIG-BIG order"
        conditions.eventually {
            device = assetStorageService.find(device.getId(), true)

            // Verify attributes receive values with the configured byte/word order
            assert device.getAttribute("int32Value").flatMap { it.getValue() }.isPresent()
            assert device.getAttribute("floatValue").flatMap { it.getValue() }.isPresent()

            // Mock server returns 0x12345678 for INT32 at 200-201 (BIG-BIG byte/word order)
            assert device.getAttribute("int32Value").flatMap { it.getValue() }.get() == 0x12345678

            // Mock server returns 12.34f for FLOAT at 202-203
            def floatValue = device.getAttribute("floatValue").flatMap { it.getValue() }.get() as Float
            assert Math.abs(floatValue - 12.34f) < 0.01f
        }
    }

    def "Modbus TCP Test - Multi-Register Write (registersAmount)"() {
        given: "expected conditions"
        def conditions = new PollingConditions(timeout: 10, delay: 0.2)

        when: "the container starts"
        def container = startContainer(defaultConfig(), defaultServices())
        def assetStorageService = container.getService(AssetStorageService.class)
        def assetProcessingService = container.getService(AssetProcessingService.class)
        def agentService = container.getService(AgentService.class)

        and: "a Modbus TCP agent is created"
        def agent = new ModbusTcpAgent("Modbus TCP Multi-Write Test")
        agent.setRealm(MASTER_REALM)
        agent.setHost("127.0.0.1")
        agent.setPort(modbusServerPort)
        agent = assetStorageService.merge(agent)

        then: "the agent should connect successfully"
        conditions.eventually {
            agent = assetStorageService.find(agent.getId())
            agent.getAttribute(Agent.STATUS).get().getValue().get() == ConnectionStatus.CONNECTED
        }

        when: "a device with multi-register write attributes is created"
        def device = new ThingAsset("Multi-Write Test Device")
        device.setRealm(MASTER_REALM)
        device.addOrReplaceAttributes(
                // Write FLOAT (2 registers) to address 300
                new Attribute<>("floatWriteValue", ValueType.NUMBER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.REAL)
                                    it.setReadAddress(300)
                                    it.setRegistersAmount(2)
                                    it.setWriteMemoryArea(ModbusAgentLink.WriteMemoryArea.HOLDING)
                                    it.setWriteAddress(300)
                                }
                        )
                ),
                // Write DINT (2 registers) to address 302
                new Attribute<>("dintWriteValue", ValueType.INTEGER).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)
                                    it.setReadMemoryArea(ModbusAgentLink.ReadMemoryArea.HOLDING)
                                    it.setReadValueType(ModbusAgentLink.ModbusDataType.DINT)
                                    it.setReadAddress(302)
                                    it.setRegistersAmount(2)
                                    it.setWriteMemoryArea(ModbusAgentLink.WriteMemoryArea.HOLDING)
                                    it.setWriteAddress(302)
                                }
                        )
                )
        )
        device = assetStorageService.merge(device)

        then: "device should be linked"
        conditions.eventually {
            device = assetStorageService.find(device.getId(), true)
            assert device.getAttribute("floatWriteValue").isPresent()
            assert device.getAttribute("dintWriteValue").isPresent()
        }

        when: "multi-register float value is written"
        latestWriteMessage.set(null)
        def floatWrite = assetProcessingService.sendAttributeEvent(new AttributeEvent(device.getId(), "floatWriteValue", 98.76f))

        then: "the write should use PLC4X array notation (indicated by register count > 1)"
        conditions.eventually {
            // Since PLC4X may write immediately or the mock server may respond differently,
            // we verify that the attribute is writable and configured correctly
            device = assetStorageService.find(device.getId(), true)
            def attr = device.getAttribute("floatWriteValue").get()
            def link = attr.getMetaItem(AGENT_LINK).get().getValue(ModbusAgentLink.class).get()

            // Verify the configuration uses 2 registers for write
            assert link.getRegistersAmount() == 2
            assert link.getWriteAddress()

            //  Check if a write message was captured (may not always happen depending on timing)
            def msg = (latestWriteMessage.get() as ModbusMessage)?.unwrap(RegistersModbusMessage)
            if (msg != null) {
                assert msg.getCount() >= 1  // At least 1 register written
            }
        }

        when: "multi-register integer value is written"
        latestWriteMessage.set(null)
        assetProcessingService.sendAttributeEvent(new AttributeEvent(device.getId(), "dintWriteValue", 123456))

        then: "the attribute should be configured for multi-register write"
        conditions.eventually {
            device = assetStorageService.find(device.getId(), true)
            def attr = device.getAttribute("dintWriteValue").get()
            def link = attr.getMetaItem(AGENT_LINK).get().getValue(ModbusAgentLink.class).get()

            // Verify the configuration uses 2 registers for write
            assert link.getRegistersAmount() == 2
            assert link.getWriteAddress()
        }
    }

    def "Modbus TCP Test - Write With Polling Rate"() {
        given: "expected conditions"
        def conditions = new PollingConditions(timeout: 15, delay: 0.2)

        when: "the container starts"
        def container = startContainer(defaultConfig(), defaultServices())
        def assetStorageService = container.getService(AssetStorageService.class)
        def assetProcessingService = container.getService(AssetProcessingService.class)
        def agentService = container.getService(AgentService.class)

        and: "a Modbus TCP agent is created"
        def agent = new ModbusTcpAgent("Modbus TCP Polling Write Test")
        agent.setRealm(MASTER_REALM)
        agent.setHost("127.0.0.1")
        agent.setPort(modbusServerPort)
        agent = assetStorageService.merge(agent)

        then: "the agent should connect successfully"
        conditions.eventually {
            agent = assetStorageService.find(agent.getId())
            agent.getAttribute(Agent.STATUS).get().getValue().get() == ConnectionStatus.CONNECTED
        }

        when: "a device with writeWithPollingRate enabled is created"
        def device = new ThingAsset("Polling Write Test Device")
        device.setRealm(MASTER_REALM)
        device.addOrReplaceAttributes(
                new Attribute<>("pollingWriteValue", ValueType.INTEGER, 42).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setPollingMillis(1000)  // Write every 500ms
                                    it.setWriteMemoryArea(ModbusAgentLink.WriteMemoryArea.HOLDING)
                                    it.setWriteAddress(400)
                                    it.setWriteWithPollingRate(true)  // Enable periodic write
                                }
                        )
                )
        )
        device = assetStorageService.merge(device)

        then: "write polling task should be created"
        conditions.eventually {
            def protocol = agentService.getProtocolInstance(agent.id) as ModbusTcpProtocol
            assert protocol != null
            assert protocol.writePollingMap.size() == 1
        }



        and: "periodic writes should occur even without attribute events"
        int writeCount = 0
        latestWriteMessage.set(null)

        conditions.eventually {
            // Should receive at least 2 writes within timeout period (500ms interval)
            if (latestWriteMessage.get() != null) {
                writeCount++
                latestWriteMessage.set(null)  // Reset for next write
            }
            assert writeCount >= 2
        }

        when: "attribute value is changed"
        device = assetStorageService.find(device.getId(), true)
        device.getAttribute("pollingWriteValue").ifPresent { attr ->
            attr.setValue(99)
        }
        device = assetStorageService.merge(device)
        Thread.sleep(1000)  // Wait for value to be stored and next polling write

        then: "the new value should be written periodically"
        latestWriteMessage.set(null)
        conditions.eventually {
            def msg = (latestWriteMessage.get() as ModbusMessage)?.unwrap(RegistersModbusMessage)
            assert msg != null
            assert msg.getAddress() == 399  // PLC4X uses 0-indexed
            assert msg.dataDecodeUnsigned()[0] == 99  // Should write the updated value
        }

        def assetDatapointService = container.getService(org.openremote.manager.datapoint.AssetDatapointService.class)
        def attributeRef = new org.openremote.model.attribute.AttributeRef(device.getId(), "pollingWriteValue")

        then: "database should only store datapoints from value changes"
        conditions.eventually {
            // Query datapoints stored for this attribute - writeWithPollingRate should no extra datapoints for writes
            def datapoints = assetDatapointService.getDatapoints(attributeRef)
            println "Datapoints found: ${datapoints.size()}"
            datapoints.each { println "  - timestamp: ${it.timestamp}, value: ${it.value}" }
            assert datapoints.size() == 2  // Should have 2 datapoint from value changes, not from periodic writes
        }
    }

    def "Modbus TCP Test - Write-Only Attribute (No Read Configuration)"() {
        given: "expected conditions"
        def conditions = new PollingConditions(timeout: 10, delay: 0.2)

        when: "the container starts"
        def container = startContainer(defaultConfig(), defaultServices())
        def assetStorageService = container.getService(AssetStorageService.class)
        def assetProcessingService = container.getService(AssetProcessingService.class)
        def agentService = container.getService(AgentService.class)

        and: "a Modbus TCP agent is created"
        def agent = new ModbusTcpAgent("Modbus TCP Write-Only Test")
        agent.setRealm(MASTER_REALM)
        agent.setHost("127.0.0.1")
        agent.setPort(modbusServerPort)
        agent = assetStorageService.merge(agent)

        then: "the agent should connect successfully"
        conditions.eventually {
            agent = assetStorageService.find(agent.getId())
            agent.getAttribute(Agent.STATUS).get().getValue().get() == ConnectionStatus.CONNECTED
        }

        when: "a device with write-only attribute is created (no read configuration)"
        def device = new ThingAsset("Write-Only Device")
        device.setRealm(MASTER_REALM)
        device.addOrReplaceAttributes(
                // Write-only attribute - no readMemoryArea, readValueType, or readAddress
                new Attribute<>("writeOnlyValue", ValueType.INTEGER, 555).addOrReplaceMeta(
                        new MetaItem<>(AGENT_LINK, new ModbusAgentLink(agent.getId())
                                .tap {
                                    it.setUnitId(1)
                                    it.setWriteMemoryArea(ModbusAgentLink.WriteMemoryArea.HOLDING)
                                    it.setWriteAddress(500)
                                    it.setPollingMillis(1000)
                                }
                        )
                )
        )
        device = assetStorageService.merge(device)

        then: "the attribute should be linked without errors"
        conditions.eventually {
            def protocol = agentService.getProtocolInstance(agent.id) as ModbusTcpProtocol
            assert protocol != null
            // No read polling tasks should be created for write-only attributes
            assert protocol.batchGroups.values().flatten().isEmpty()
        }

        when: "a write operation is performed"
        latestWriteMessage.set(null)
        assetProcessingService.sendAttributeEvent(new AttributeEvent(device.getId(), "writeOnlyValue", 777))

        then: "the write should be sent successfully"
        conditions.eventually {
            def msg = (latestWriteMessage.get() as ModbusMessage)?.unwrap(RegistersModbusMessage)
            assert msg != null
            assert msg.getUnitId() == 1
            assert msg.getAddress() == 499  // PLC4X uses 0-indexed (500-1)
            assert msg.dataDecodeUnsigned()[0] == 777  // Written value
        }
    }

    def "Modbus TCP Test - Partial Read Configuration Should Be Ignored"() {
        given: "expected conditions"
        def conditions = new PollingConditions(timeout: 10, delay: 0.2)

        when: "the container starts"
        def container = startContainer(defaultConfig(), defaultServices())
        def assetStorageService = container.getService(AssetStorageService.class)
        def agentService = container.getService(AgentService.class)

        and: "a Modbus TCP agent is created"
        def agent = new ModbusTcpAgent("Modbus TCP Partial Config Test")
        agent.setRealm(MASTER_REALM)
        agent.setHost("127.0.0.1")
        agent.setPort(modbusServerPort)
        agent = assetStorageService.merge(agent)

        then: "the agent should connect successfully"
        conditions.eventually {
            agent = assetStorageService.find(agent.getId())
            agent.getAttribute(Agent.STATUS).get().getValue().get() == ConnectionStatus.CONNECTED
        }



        and: "agent should remain in CONNECTED state (no exceptions thrown)"
        conditions.eventually {
            agent = assetStorageService.find(agent.getId())
            assert agent.getAttribute(Agent.STATUS).get().getValue().get() == ConnectionStatus.CONNECTED
        }
    }

}
