apply plugin: "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"

base {
    archivesName = "openremote-${project.name}"
}

dependencies {
    implementation project(":manager")
    implementation project(":extension:energy")
}

jar {
    from sourceSets.main.allJava
}

javadoc {
    failOnError = false
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            group = "io.openremote.extension"
            artifactId = "openremote-${project.name}"
            from components.java
            pom {
                name = 'OpenRemote Demo setup'
                description = 'Adds the OpenRemote Demo setup'
                url = 'https://github.com/openremote/openremote'
                licenses {
                    license {
                        name = 'GNU Affero General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'developers'
                        name = 'Developers'
                        email = 'developers@openremote.io'
                        organization = 'OpenRemote'
                        organizationUrl = 'https://openremote.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/openremote/openremote.git'
                    developerConnection = 'scm:git:ssh://github.com:openremote/openremote.git'
                    url = 'https://github.com/openremote/openremote/tree/master'
                }
            }
        }
    }

    repositories {
        maven {
            if (!version.endsWith('-LOCAL')) {
                credentials {
                    username findProperty("publishUsername")
                    password findProperty("publishPassword")
                }
            }
            url = version.endsWith('-LOCAL') ? layout.buildDirectory.dir('repo') : version.endsWith('-SNAPSHOT') ? findProperty("snapshotsRepoUrl") : findProperty("releasesRepoUrl")
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    if (signingKey && signingPassword) {
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

tasks.register('installDist', Copy) {
    dependsOn resolveTask(":manager:installDist")
    onlyIf { !project.hasProperty("SETUP_JAR") || project.getProperties().get("SETUP_JAR") == "demo" }
    from jar.archiveFile
    into project(":manager").layout.buildDirectory.dir("install/manager/deployment/manager/extensions")
}
