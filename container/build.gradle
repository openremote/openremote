apply plugin: "java-library"
apply plugin: "groovy"
apply plugin: "maven-publish"
apply plugin: "signing"

dependencies {

    api resolveProject(":model")

    api "org.codehaus.groovy:groovy-all:$groovyVersion"
    api "org.craftercms:groovy-sandbox:$groovySandboxVersion"

    api "org.slf4j:slf4j-api:$slf4jVersion"
    api "org.slf4j:slf4j-jdk14:$slf4jVersion"
    api "org.slf4j:jcl-over-slf4j:$slf4jVersion"
    api "org.slf4j:log4j-over-slf4j:$slf4jVersion"

    api "com.devskiller.friendly-id:friendly-id:$friendlyIdVersion"

    api "io.undertow:undertow-servlet:$undertowVersion"
    api "io.undertow:undertow-websockets-jsr:$undertowVersion"

    api "org.jboss.resteasy:resteasy-jaxrs:$resteasyVersion"
    api "org.jboss.resteasy:resteasy-client:$resteasyVersion"
    api "org.jboss.resteasy:resteasy-jackson2-provider:$resteasyVersion"
    api "org.jboss.resteasy:resteasy-multipart-provider:$resteasyVersion"
    api("org.jboss.resteasy:resteasy-cache-core:$resteasyVersion") {
        exclude group: "org.infinispan"
    }
    api "org.jboss.resteasy:resteasy-validator-provider:$resteasyVersion"
    api "org.jboss.resteasy:async-http-servlet-3.0:$resteasyVersionAsyncHttpServlet"

    api "org.keycloak:keycloak-client-registration-api:$keycloakVersion"
    api "org.keycloak:keycloak-undertow-adapter:$keycloakVersion"
    api "org.keycloak:keycloak-admin-client:$keycloakVersion"

    api "org.postgresql:postgresql:$postgresJdbcVersion"
    api "org.flywaydb:flyway-core:$flywaydbVersion"
    api "com.zaxxer:HikariCP:$hikaricpVersion"
    api "org.hibernate:hibernate-core:$hibernateVersion"
    api "org.hibernate:hibernate-validator:$hibernateValidatorVersion"

    api "javax.el:javax.el-api:3.0.0"

    api "org.apache.camel:camel-core:$camelVersion"
    api "org.apache.camel:camel-jackson:$camelVersion"
    api "org.apache.camel:camel-snmp:$camelVersion"
    api "org.apache.camel:camel-health:$camelVersion"

    api("com.fasterxml.uuid:java-uuid-generator:$uuidGeneratorVersion") {
        exclude group: "log4j"
    }

    api("org.cyclopsgroup:jmxterm:$jmxtermVersion") {
        exclude group: "javax.servlet"
        exclude group: "log4j"
        exclude group: "logkit"
        exclude group: "commons-logging"
    }

    api "com.vladmihalcea:hibernate-types-52:$hibernateTypesVersion"
}

jar {
    archivesBaseName = "openremote-${project.name}"
}

javadoc {
    failOnError = false
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            group = "io.openremote"
            artifactId = "openremote-${project.name}"
            from components.java
            pom {
                name = 'OpenRemote Container'
                description = 'Application container for running; add maven {url "https://repo.osgeo.org/repository/release/"} and maven {url "https://pkgs.dev.azure.com/OpenRemote/OpenRemote/_packaging/OpenRemote/maven/v1"} to resolve all dependencies'
                url = 'https://github.com/openremote/openremote'
                licenses {
                    license {
                        name = 'GNU Affero General Public License v3.0'
                        url = 'https://www.gnu.org/licenses/agpl-3.0.en.html'
                    }
                }
                developers {
                    developer {
                        id = 'developers'
                        name = 'Developers'
                        email = 'developers@openremote.io'
                        organization = 'OpenRemote'
                        organizationUrl = 'https://openremote.io'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/openremote/openremote.git'
                    developerConnection = 'scm:git:ssh://github.com:openremote/openremote.git'
                    url = 'https://github.com/openremote/openremote/tree/master'
                }
            }
        }
    }

    repositories {
        maven {
            if (!version.endsWith('-LOCAL')) {
                credentials {
                    username findProperty("publishUsername")
                    password findProperty("publishPassword")
                }
            }
            url = version.endsWith('-LOCAL') ? layout.buildDirectory.dir('repo') : version.endsWith('-SNAPSHOT') ? findProperty("snapshotsRepoUrl") : findProperty("releasesRepoUrl")
        }
    }
}

signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}
