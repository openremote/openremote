apply plugin: "java-library"

var setupJar = project.hasProperty("SETUP_JAR") ? "${project.getProperties().get("SETUP_JAR")}" : "demo"

base {
    archivesName = "openremote-${setupJar}-${project.name}"
}

sourceSets {
    integration
    demo
    load1
}

// dependencies declaration
configurations {
    integrationImplementation.extendsFrom(implementation)
    demoImplementation.extendsFrom(implementation)
    load1Implementation.extendsFrom(implementation)
}

dependencies {
    implementation project(":manager")
}


tasks.register('load1Jar', Jar) {
    archiveBaseName.set("openremote-load1-${project.name}")
    from sourceSets.load1.output
}

tasks.register('demoJar', Jar) {
    archiveBaseName.set("openremote-demo-${project.name}")
    from sourceSets.demo.output
}

// Use demo setup sourceset by default for jar
tasks.register('installDist', Copy) {
    def setupTask = getTasksByName("${setupJar}Jar", false)[0] as org.gradle.jvm.tasks.Jar
    System.out.println(setupTask.name)
    dependsOn setupTask
    dependsOn resolveTask(":manager:installDist")
    from setupTask.archiveFile
    into "${project(":manager").buildDir}/install/manager/deployment/manager/extensions"
}
