apply plugin: "java-library"

var setupJar = project.hasProperty("SETUP_JAR") ? "${project.getProperties().get("SETUP_JAR")}" : "demo"

base {
    archivesName = "openremote-${setupJar}-${project.name}"
}

sourceSets {
    integration
    demo
    load1
    load2
}

// dependencies declaration
configurations {
    integrationImplementation.extendsFrom(implementation)
    demoImplementation.extendsFrom(implementation)
    load1Implementation.extendsFrom(implementation)
    load2Implementation.extendsFrom(implementation)
}

dependencies {
    implementation project(":manager")
}


tasks.register('load1Jar', Jar) {
    archiveBaseName.set("openremote-load1-${project.name}")
    from sourceSets.load1.output
}

tasks.register('load2Jar', Jar) {
    archiveBaseName.set("openremote-load2-${project.name}")
    from sourceSets.load2.output
}

tasks.register('demoJar', Jar) {
    archiveBaseName.set("openremote-demo-${project.name}")
    from sourceSets.demo.output
}

tasks.named("compileJava") {
    dependsOn("load1Jar", "load2Jar", "demoJar")
}

tasks.register('runDemo', JavaExec) {
    group = 'application'
    description = 'Runs the Manager with Demo Setup'
    classpath = sourceSets.demo.runtimeClasspath
    mainClass = 'org.openremote.manager.Main'
    standardOutput = System.out
    errorOutput = System.err
    environment 'OR_SETUP_TYPE', 'demo'
    // Default env vars, can be overridden
    environment 'OR_DB_HOST', System.getenv('OR_DB_HOST') ?: 'localhost'
    environment 'OR_KEYCLOAK_HOST', System.getenv('OR_KEYCLOAK_HOST') ?: 'localhost'
}

tasks.register('runTest', JavaExec) {
    group = 'application'
    description = 'Runs the Manager with Test Setup'
    classpath = sourceSets.integration.runtimeClasspath
    mainClass = 'org.openremote.manager.Main'
    // Default env vars
    environment 'OR_DB_HOST', System.getenv('OR_DB_HOST') ?: 'localhost'
    environment 'OR_KEYCLOAK_HOST', System.getenv('OR_KEYCLOAK_HOST') ?: 'localhost'
}

// Use demo setup sourceset by default for jar
tasks.register('installDist', Copy) {
    def setupTask = getTasksByName("${setupJar}Jar", false)[0] as org.gradle.jvm.tasks.Jar
    System.out.println(setupTask.name)
    dependsOn setupTask
    dependsOn resolveTask(":manager:installDist")
    from setupTask.archiveFile
    into project(":manager").layout.buildDirectory.dir("install/manager/deployment/manager/extensions")
}
