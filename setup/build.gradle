apply plugin: "java-library"

var setupJar = project.hasProperty("SETUP_JAR") ? project.getProperties().get("SETUP_JAR") : "demo"

base {
    archivesName = "openremote-${setupJar}-${project.name}"
}

sourceSets {
    integration
    load1
    load2
}

// dependencies declaration
configurations {
    integrationImplementation.extendsFrom(implementation)
    load1Implementation.extendsFrom(implementation)
    load2Implementation.extendsFrom(implementation)
}

dependencies {
    implementation project(":manager")
}

tasks.register('load1Jar', Jar) {
    archiveBaseName.set("openremote-load1-${project.name}")
    from sourceSets.load1.output
}

tasks.register('load2Jar', Jar) {
    archiveBaseName.set("openremote-load2-${project.name}")
    from sourceSets.load2.output
}

tasks.named("compileJava") {
    dependsOn("load1Jar", "load2Jar")
}

tasks.register('installDist', Copy) {
    def setupTask = getTasksByName("${setupJar}Jar", false)[0] as org.gradle.jvm.tasks.Jar
    if (setupTask != null) {
        System.out.println(setupTask.name)
        dependsOn setupTask
        dependsOn resolveTask(":manager:installDist")
        from setupTask.archiveFile
        into project(":manager").layout.buildDirectory.dir("install/manager/deployment/manager/extensions")
    }
}
